// Raw Array of the classes for the `Carbon.PrimaryContent:PrimaryContent`.
// Can be used also for the `body` tag
prototype(Carbon.PrimaryContent:ClassArray) < prototype(Neos.Fusion:RawArray) {
    config = ${Configuration.setting('Carbon.PrimaryContent')}

    namespace = ${this.config.class.namespace}
    component = ${this.config.class.component}
    prepend = ${this.config.class.prepend}
    depth = ${documentNode.depth - 2}
    subpagesFilter = ${this.config.subpagesFilter}

    @ignoreProperties = ${['config', 'namespace', 'component', 'prepend', 'depth', 'subpagesFilter']}
    @context.prepend = ${this.prepend}

    element = ${this.namespace + '-' + this.component}
    level = ${this.prepend + (this.depth == 0 ? 'index' : 'deep' + this.depth)}
    subpages = ${this.prepend + (q(documentNode).children(this.subpagesFilter).filter('[_hiddenInIndex=false]').is() ? 'hassub' : 'nosub')}
    subpages.@if.isNotIndex = ${!!this.depth}
    type = Carbon.PrimaryContent:NodeTypeName {
        node = ${documentNode}
        prepend = ${prepend}
    }
}
